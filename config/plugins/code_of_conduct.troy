# Handle Code of Conduct acceptance. If a user adds ✅ to our rules message they accept the code of conduct
# if they remove it, they are considered to un-accept it

define pipeline discord_plugin
pipeline
  use discord::guild;
  use config;
  select guild::add_remove_roles(event.AddReaction, [config::accepted_role], [config::not_accepted_role]) from in where
    match event of
      case %{AddReaction ~= %{message_id == config::code_of_conduct_msg, emoji ~= %{name == "✅"}}} => true
      default => false
    end
  into out;

  select guild::add_remove_roles(event.RemoveReaction, [config::not_accepted_role], [config::accepted_role]) from in where
    match event of
      case %{RemoveReaction ~= %{message_id == config::code_of_conduct_msg, emoji ~= %{name == "✅"}}} => true
      default => false
    end
  into out;

  # Add roles to new members to the not accepted roles
  select guild::add_roles({"guild_id": event.`MemberAddition`.new_member.guild_id, "user_id": event.`MemberAddition`.new_member.user.id}, [config::not_accepted_role]) from in where
    match event of
    case %{present MemberAddition} => true
      default => false
    end
  into out;
end;
